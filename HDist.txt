model{

## likelihood/sampling model for the basket trials:
	for(i in 1:Nobs){

		y[i] ~ dnorm(m.y[i], tau.y)
		m.y[i] <- z1[i]*gamma1[Module[i]] + z2[i]*gamma2[Module[i]] + Trt[i]*theta[Module[i]]

	}

	for(k in 1:kMod){

		gamma1[k] ~ dnorm(mu.g1, tau.g1)
		gamma2[k] ~ dnorm(mu.g2, tau.g2)
		
		theta[k] <- Prior.theta[k] + Prior.sd.theta[k]*theta01[k]
		theta01[k] ~ dnorm(0, 1)

		for(q in 1:kMod){
			phiH[q,k] <- sqrt(1/2*pow(sqrt(abs(theta[k])) - sqrt(abs(theta[q])), 2))
			norm.phiH[q,k] <- exp(-phiH[q,k]/s0)

			wss[q,k] <- phiH[q,k] - equals(q,k)

			D01[q,k] ~ dunif(0, 1)
			nu0[q,k] <- lSlab + (uSlab - lSlab)*1/wss[q,k]*D01[q,k]
			nu[q,k] <- nu0[q,k]*step(wss[q,k] - nu0[q,k]) + spike*step(nu0[q,k] - wss[q,k])

				pred.mu.theta[q,k] <- theta[k] + cms.mu[q,k]*1/nu[q,k]*(1-equals(q,k))
				cms.mu[q,k] ~ dnorm(0, 1)
		
		}

		sum.norm.phiH[k] <- sum(norm.phiH[k,]) - 1

		for(q in 1:kMod){
			p0[q,k] <- norm.phiH[q,k]/sum.norm.phiH[q] 
			V[q,k] <- 1 - equals(q, k)
			pmix[q,k] <- inprod(p0[q,k], V[q,k])
			r.theta.star[q, k] <- inprod(pmix[q,k], pred.mu.theta[q,k])
		}

	theta.star[k] <- sum(r.theta.star[k,])

	}

		# priors
		tau.y ~ dgamma(0.5, 0.005)

		mu.g1 ~ dnorm(0, 0.1)
		mu.g2 ~ dnorm(0, 0.1)

		tau.g1 ~ dgamma(0.001, 0.001)
		tau.g2 ~ dgamma(0.001, 0.001)

	# Inference for the module specific parameter
	for(i in 1:Nobs.star){
		y.star[i] ~ dnorm(m.y.star[i], tau.y)
		m.y.star[i] <- z1.star[i]*gamma1[Module.star[i]] + z2.star[i]*gamma2[Module.star[i]] + Trt.star[i]*theta.star[Module.star[i]]
	}

	for(k in 1:kMod){
		pCat[k, 1] <- 1 - step(sig.theta[1] - theta.star[k])
		pCat[k, 2] <- 1 - step(sig.theta[2] - theta.star[k])
	}

}
