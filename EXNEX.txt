model{

## likelihood/sampling model for the basket trials:
	for(i in 1:Nobs){

		y[i] ~ dnorm(m.y[i], tau.y)
		m.y[i] <- z1[i]*gamma1[Module[i]] + z2[i]*gamma2[Module[i]] + Trt[i]*theta[Module[i]]

	}


	for(k in 1:kMod){

		gamma1[k] ~ dnorm(mu.g1, tau.g1)
		gamma2[k] ~ dnorm(mu.g2, tau.g2)
		
		mix.theta[1, k] <- mu.theta + re.theta[k]
		re.theta[k] ~ dnorm(0, prec.re)	

		mix.theta[2, k]	~ dnorm(nex.theta, nex.prec)

		exch.ind[k] ~ dcat(pMix[1:2])
			for(j in 1:2){
				each[k, j] <- equals(exch.ind[k], j)		
			}

		# pick theta
		theta[k] <- mix.theta[exch.ind[k], k]

		pCat[k, 1] <- 1 - step(sig.theta[1] - theta[k])
		pCat[k, 2] <- 1 - step(sig.theta[2] - theta[k])

	}

		# priors
		tau.y ~ dgamma(0.5, 0.005)
		mu.g1 ~ dnorm(0, 0.1)
		mu.g2 ~ dnorm(0, 0.1)
		tau.g1 ~ dgamma(0.001, 0.001)
		tau.g2 ~ dgamma(0.001, 0.001)

		mu.theta ~ dnorm (Prior.mu.theta[1], prec.mu.theta)
		prec.mu.theta <- pow(Prior.mu.theta[2], -2)
		
		prec.re <- pow(tau, -2)
		tau ~ dnorm(0, prec.sig)I(0.001,)
		prec.sig <- pow(Prior.tau.HN, -2)

		nex.prec <- pow(nex.sig, -2)


}

